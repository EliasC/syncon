// This file contains an attempt at defining the syntax of a syncon
// language definition, using a syncon language definition. Note that
// it does not cover bindings nor spliced syntax in the body

type Top

// Posix character classes in token definitions
token Name = "[[:lower:]][[:word:]]*"
token TypeName = "[[:upper:]][[:word:]]*"
token String = "\"(\\\\.|[^\"\\\\])*\""

comment "//[^\\n]*(\\n|$)"

// === Syntax Types ===

syncon typeDef: Top =
  "type" syType:TypeName
{ builtin }

// === Token Definitions ===

syncon tokenDef: Top =
  "token" syType:TypeName "=" regex:String
{ builtin }

// === Comment Definitions ===

syncon commentDef: Top =
  "comment" regex:String
{ builtin }

// === Syncon Definitions (including operators) ===

syncon synconDef: Top =
  "syncon" name:Name ":" syType:TypeName "="
  descr:SyntaxDescription+ "{" (Property ";")* "builtin" "}"
{ builtin }

syncon prefixDef: Top =
  "prefix" name:Name ":" syType:TypeName "="
  descr:SyntaxDescription+ "{" (Property ";")* "builtin" "}"
{ builtin }

syncon postfixDef: Top =
  "postfix" name:Name ":" syType:TypeName "="
  descr:SyntaxDescription+ "{" (Property ";")* "builtin" "}"
{ builtin }

syncon infixDef: Top =
  "infix" assoc:("left" | "right")? name:Name ":" syType:TypeName "="
  descr:SyntaxDescription* "{" (Property ";")* "builtin" "}"
{ builtin }


// === Properties ===

type Property


// === Syntax Descriptions ===

type SyntaxDescription

// TODO: This might be problematic?
syncon sdSeq: SyntaxDescription =
  "(" head:SyntaxDescription tail:SyntaxDescription+ ")"
{ builtin }

postfix sdStar: SyntaxDescription = "*" { builtin }
postfix sdPlus: SyntaxDescription = "+" { builtin }
postfix sdQuestion: SyntaxDescription = "?" { builtin }

syncon sdAlt: SyntaxDescription = left:rec+ ("|" right:rec+)+ { builtin }
forbid sdAlt.left = sdAlt
forbid sdAlt.right = sdAlt

prefix sdName: SyntaxDescription = name:(Name | "left" | "right") ":" { builtin }

precedence {
  sdName;
  sdStar sdPlus sdQuestion;
  sdAlt;
}

syncon sdLitToken: SyntaxDescription = tok:String { builtin }
syncon sdSyTy: SyntaxDescription = name:TypeName { builtin }
syncon sdRec: SyntaxDescription = "rec" { builtin }


// === Disambiguation ===

syncon forbidDef: Top =
  "forbid" syName:Name "." sdName:(Name | "left" | "right") "=" syName2:Name
{ builtin }

syncon precedenceList: Top =
  "precedence" "{" line:(name:Name+ ";")+ "}"
  ("except" "{" eLine:(eName:Name+ ";")+ "}")?
{ builtin }
